BEGIN;

-- return row of nulls when user is not found
CREATE OR REPLACE FUNCTION userrec(username TEXT) RETURNS SETOF users AS $$
DECLARE
        n TEXT;
        d TEXT;
        pos INTEGER;
        r RECORD;
BEGIN
        pos := position('@' in username);
        IF pos > 0 THEN
                n := substring(username for pos - 1);
                d := substring(username from pos + 1);
                SELECT * INTO r FROM users WHERE num = n AND domain = d;
        ELSE
                SELECT * INTO r FROM users WHERE num = username LIMIT 1;
        END IF;
        IF NOT FOUND THEN
                r := ROW( NULL::INTEGER, NULL::PHONE, NULL::TEXT, NULL::TEXT, NULL::TEXT, NULL::TIMESTAMP WITH TIME ZONE, NULL::INET, NULL::BOOLEAN, NULL::BOOLEAN, NULL::TEXT);
        END IF;
        RETURN NEXT r;
END;
$$ LANGUAGE PlPgSQL STRICT STABLE;

CREATE UNIQUE INDEX callgroups_num_index ON callgroups(num);

CREATE OR REPLACE FUNCTION callgroups_route(called_arg TEXT)
	RETURNS TABLE(key TEXT, value TEXT) AS $$
DECLARE
	g RECORD;
	t RECORD;
	res HSTORE;
	cntr INTEGER;
BEGIN
	SELECT * INTO g FROM callgroups WHERE num = called_arg;
	IF NOT FOUND THEN
		RETURN;
	END IF;

	res := 'location => fork';
	cntr := 0;
	FOR t IN SELECT num FROM callgrpmembers WHERE grp = g.id ORDER BY ord LOOP
		cntr := cntr + 1;
		res := res || hstore('callto.' || cntr, 'lateroute/' || t.num);
	END LOOP;

	IF cntr = 0 THEN
		res := 'location => "", error => "offline"';
	END IF;
	RETURN QUERY SELECT * FROM each(res);
END;
$$ LANGUAGE PlPgSQL;

CREATE OR REPLACE FUNCTION regs_route(caller_arg TEXT, called_arg TEXT, ip_host_arg INET, formats_arg TEXT, rtp_forward_arg TEXT)
	RETURNS TABLE(key TEXT, value TEXT) AS $$
DECLARE
	-- clr RECORD;
	clduid INTEGER;
	t RECORD;
	res HSTORE;
	cntr INTEGER;
BEGIN
	-- clr := userrec(caller_arg);
	clduid := userid(called_arg);
	IF clduid IS NULL THEN
		RETURN;
	END IF;

	res := 'location => fork';
	cntr := 0;
	FOR t IN SELECT * FROM regs WHERE userid = clduid AND expires > 'now' AND audio LOOP
		cntr := cntr + 1;
		res := res || hstore('callto.' || cntr, t.location);
		res := res || hstore('callto.' || cntr || '.rtp_forward', CASE WHEN rtp_forward_arg = 'possible' THEN rtp_forward_possible(ip_host_arg, t.ip_host) ELSE 'no' END);
	END LOOP;

	IF cntr = 0 THEN
		-- res := 'location => "", error => "offline"';
		RETURN;
	END IF;
	RETURN QUERY SELECT * FROM each(res);
END;
$$ LANGUAGE PlPgSQL;

CREATE OR REPLACE FUNCTION route_master(msg HSTORE) RETURNS TABLE(field TEXT, value TEXT) AS $$
BEGIN
	RETURN QUERY
		SELECT * FROM regs_route(msg->'caller', msg->'called', (msg->'ip_host')::INET, msg->'formats', msg->'rtp_forward')
	UNION
		SELECT * FROM callgroups_route(msg->'called');
END;
$$ LANGUAGE PlPgSQL;

COMMIT;
-- vim: ft=sql
